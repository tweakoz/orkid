///////////////////////////////////////////////////////////////
// FxConfigs
///////////////////////////////////////////////////////////////
fxconfig fxcfg_default {
  import "orkshader://mathtools.i";
  import "orkshader://pbrtools.i";
  import "orkshader://envtools.i";
}
///////////////////////////////////////////////////////////////
uniform_set ub_frgX {
  float Time;
  sampler3D MapVolTexA;
  sampler2D envmap;
  sampler2D refract_map;
  sampler2D depth_map;
  vec3 BaseColor;
  vec3 ColorB;
  vec3 ColorC;
  vec3 ColorD;
  float GroundColorPower;
  vec2 bufinvdim;

}
///////////////////////////////////////////////////////////////
libblock lib_ground_vtx {

  void vs_common_mono(vec4 pos, vec3 nrm, vec3 bin) {
    vec4 cpos       = v * pos;
    vec4 wpos       = m * pos;
    vec3 wnormal    = normalize(mrot * normal);
    vec3 wbitangent = normalize(mrot * binormal); // technically binormal is a bitangent
    vec3 wtangent   = cross(wbitangent, wnormal);
    frg_wpos        = wpos;
    frg_clr         = vec4(1, 1, 1, 1); // TODO - split vs_rigid_gbuffer into vertexcolor vs identity
    frg_uv0         = uv0 * vec2(1, -1);
    frg_tbn         = mat3(wtangent, wbitangent, wnormal);

    // compute eye to worldpos direction vector
    frg_camz = vec3(0,1,0);

    frg_camdist     = -cpos.z;
  }

  void vs_common_stereo(vec4 pos, vec3 nrm, vec3 bin) {
    vec4 wpos       = m * pos;
    vec4 cpos_l       = (v_l*m) * pos;
    vec4 cpos_r       = (v_r*m) * pos;
    vec3 cpos = (cpos_l.xyz+cpos_r.xyz)*0.5;
    vec3 wnormal    = normalize(mrot * normal);
    vec3 wbitangent = normalize(mrot * binormal); // technically binormal is a bitangent
    vec3 wtangent   = cross(wbitangent, wnormal);
    frg_wpos        = m * pos;
    frg_clr         = vec4(1, 1, 1, 1); // TODO - split vs_rigid_gbuffer into vertexcolor vs identity
    frg_uv0         = uv0 * vec2(1, -1);
    frg_tbn         = mat3(wtangent, wbitangent, wnormal);
    //frg_camz        = wnormal.xyz;

    // compute eye to worldpos direction vector
    frg_camz = normalize(cpos.xyz - wpos.xyz);
    frg_camdist     = -cpos.z;
  }

}
///////////////////////////////////////////////////////////////
libblock lib_ground_frg
 : ub_frgX 
	: lib_fwd
	: lib_brdf
	: lib_envmapping
	: lib_def
  : lib_math {

float wave_fn(vec3 center, vec3 pos, float timeval, float frq, float baseamp){
    float radius = length(pos.xz - center.xz);
    float amp = clamp( pow(1 / radius,1.5), 0, 1);
    return sin(radius * frq - timeval) * baseamp * 0.0254 * amp;
}

vec4 gen_water_(vec2 UV, vec3 eye_pos) {
    float waveHeight = 0.0254*0.125; // 1 inch in meters


    vec3 wpos       = frg_wpos.xyz;

    vec3 eye_delta = wpos-eye_pos;
    vec3 eye_dir = normalize(eye_delta);
    vec3 skybox_n = vec3(eye_dir.x,eye_dir.y,eye_dir.z)*-1;
    vec3 skyboxColor = env_equirectangularFlipV(skybox_n,MapSpecularEnv,EnvironmentMipBias)*SkyboxLevel;

    float fogZ = length(eye_delta); 
    
    float depth_fogval = 1.0-clamp(pow(fogZ/DepthFogDistance,DepthFogPower),0,1);

    vec3 pos = wpos;
    // World space position

    float speed = Time * 2.0;
    float baseamp = 10;

    vec3 wave_pos = wpos*0.01;

    float radius = length(wave_pos.xz);

    pos.y += wave_fn(vec3(0,0,0), wave_pos, speed*1.0, 6.28*0.5, baseamp);
    pos.y += wave_fn(vec3(1,0,0), wave_pos, speed*1.5, 6.28*1.0, baseamp);
    pos.y += wave_fn(vec3(-1,0,0), wave_pos, speed*2.0, 6.28*1.5, baseamp);
    pos.y += wave_fn(vec3(0,0,1), wave_pos, speed*2.5, 6.28*2.0, baseamp);
    pos.y += wave_fn(vec3(0,0,-1), wave_pos, speed*3.0, 6.28*2.5, baseamp);

    // Create normal from screen space derivatives
    vec3 dPdu = dFdx(pos);
    vec3 dPdv = dFdy(pos);
    vec3 normal = normalize(cross(dPdu, dPdv)); // Corrected normal calculation

    vec3 skybox_refract_n = refract(skybox_n*-1.0,normal*-1.0,0.5);
    vec3 skyboxRefractColor = (vec3(1)-env_equirectangular(skybox_refract_n,MapSpecularEnv,0.8))*0.2;
    skyboxRefractColor.x = pow(skyboxRefractColor.x,0.65);
    skyboxRefractColor.y = pow(skyboxRefractColor.y,0.55);
    skyboxRefractColor.z = pow(skyboxRefractColor.z,0.45);
    float F = fresnelSchlick(eye_dir*-1.0, normal, vec3(0.004)).x;
    F = 0.5;//pow(F, 0.1);
    vec3 albedo = BaseColor;
    vec3 altcolor = skyboxRefractColor; //normal*0.5+vec3(0.5,0.5,0.5);
    albedo = mix(albedo,albedo+altcolor*0.5,1-F);
    PbrData pbd;
    pbd._emissive = false;
    pbd._metallic = 1;
    pbd._roughness = 0.5+radius*0.003;
    pbd._albedo = albedo/(1+radius*0.01);
    pbd._wpos = wpos;
    pbd._wnrm = normal;
    pbd._fogZ = 0.0;
    pbd._atmos = 0.0;
    pbd._alpha = 1.0;

    vec3 env_lighting = pbrEnvironmentLightingXXX(pbd,eye_pos);

    ///////////////////////////////////////////////
    // point lighting
    ///////////////////////////////////////////////

    LightCtx plc = lcalc_forward(wpos,pbd);
    vec3 point_lighting       = vec3(0, 0, 0);
    for(int i=0; i<point_light_count; i++){
      plc._lightdel = _lightpos[i].xyz - wpos;
      vec3 LC = _lightcolor[i].xyz*_lightcolor[i].w;
      float LR = _lightradius[i];
      point_lighting += plcalc_forward(plc,pbd,LR)*LC*0.05;
    }

    vec2 uv =  gl_FragCoord.xy * bufinvdim;

    vec3 L = env_lighting+point_lighting;
    vec3 out_color = mix(L,skyboxColor,1.0-depth_fogval);
    float depth = textureLod(depth_map,uv,0).r;
    float ndc_z = depth*2.0 - 1.0;
    float ndc_x = (uv.x * 2.0 - 1.0);
    float ndc_y = (uv.y * 2.0 - 1.0);
    vec4 clip = vec4(ndc_x,ndc_y,ndc_z,1.0);
    vec4 wld = inverse(vp) * clip;
    wld /= wld.w;

    float D = clamp( length(wpos-wld.xyz)*0.01, 0, 1 );
    D = pow(D, 0.25);

    return vec4(out_color, D);
}
}

libblock lib_ground_mono {

  vec4 gen_water_mono(vec2 UV) {
    return gen_water_(UV,EyePostion);
  }

}

libblock lib_ground_stereo {

  vec4 gen_water_stereo(vec2 UV) {
    vec3 eyepos = bool(gl_ViewportIndex) ? EyePostionR : EyePostionL;
    return gen_water_(UV,eyepos);
  }

}

///////////////////////////////////////////////////////////////
vertex_shader vs_ground_mono //
  : iface_vgbuffer //
  : lib_ground_vtx { //
  // mv, mrot, m, mvp_l, mvp_r
  vs_common_mono(position, normal, binormal);
  gl_Position                   = mvp * position;
}
///////////////////////////////////////////////////////////////
vertex_shader vs_ground_stereo //
  : iface_vgbuffer_stereo //
  : lib_ground_vtx //
  : extension(GL_NV_stereo_view_rendering) //
  : extension(GL_NV_viewport_array2) { //
  // mv, mrot, m, mvp_l, mvp_r
  vs_common_stereo(position, normal, binormal);
  gl_Position                   = mvp_l * position;
  gl_SecondaryPositionNV        = mvp_r * position;
  gl_Layer                      = 0;
  gl_ViewportMask[0]            = 1;
  gl_SecondaryViewportMaskNV[0] = 2;
}
///////////////////////////////////////////////////////////////
fragment_shader ps_fwd_ground_mono
  : iface_forward
  : lib_ground_frg
  : lib_fwd 
  : lib_ground_mono
  : ub_frgX {
    out_color = gen_water_mono(frg_uv0);
}
///////////////////////////////////////////////////////////////
fragment_shader ps_fwd_ground_stereo
  : iface_forward
  : lib_ground_frg
  : lib_ground_stereo
  : ub_frgX {
    out_color = gen_water_stereo(frg_uv0);
}
///////////////////////////////////////////////////////////////
technique FWD_DEPTHPREPASS_RI_NI_MO {
	fxconfig=fxcfg_default;
	vf_pass={vs_forward_depthprepass_mono,ps_forward_depthprepass,sb_default}
}
///////////////////////////////////////////////////////////////
technique FWD_CT_NM_RI_NI_MO {
  fxconfig=fxcfg_default;
  vf_pass={vs_ground_mono,ps_fwd_ground_mono,sb_default}
}
///////////////////////////////////////////////////////////////
technique FWD_CT_NM_RI_NI_ST {
  fxconfig=fxcfg_default;
  vf_pass={vs_ground_stereo,ps_fwd_ground_stereo,sb_default}
}